#! /bin/zsh

# keybr — work through progressively more letters

minwpm=${TTYP_MINWPM-50} # a little slower than default for this exercise

autoload -U colors; colors

print 'Keybr-like exercise to work through new letters, one at a time, starting with eniarl.\n'

export TTYP_ACTIVITY=keybr
export TTYP_ACTID=$(date '+%s')
export TTYP_MINWPM=$minwpm
export TTYP_POOLBAND=10000

trap     dostats SIGINT
function dostats { ttypist-stats keybr; }

for  c in l t o s u d y c g h p m k b w f z v k x q j
do   # Repeat test till sufficiently fast
     print "\n$fg_bold[blue]New letter: $c$reset_color"
     while ! TTYP_PATTERN='^['eniar$c${(j::)accum}']*'$c'['eniar$c${(j::)accum}']*$' ttypist
     do    print "$fg_bold[red]TOO SLOW — REPEAT\n$reset_color"; done
     accum+=$c
done
